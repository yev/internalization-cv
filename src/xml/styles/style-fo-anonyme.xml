<?xml version="1.0" encoding="utf-8" ?>
<xsl:stylesheet version="1.0"
	xmlns:xsl="http://www.w3.org/1999/XSL/Transform">
	<xsl:output method="xml" />

	<xsl:template match="/cv">
		<fo:root xmlns:fo="http://www.w3.org/1999/XSL/Format">
			<fo:layout-master-set>
				<fo:simple-page-master master-name="titreGH"
					page-height="29.7cm" page-width="21cm" margin-top="1cm"
					margin-bottom="1.5cm" margin-left="1.5cm" margin-right="1.5cm" >
					<fo:region-body margin-top="1cm" />
					<fo:region-before region-name="header" />
					<fo:region-after region-name="footer"/>
				</fo:simple-page-master>
			</fo:layout-master-set>


			<fo:page-sequence master-reference="titreGH" font-family="Georgia">
				<fo:static-content flow-name="footer">
		            <fo:block text-align="center" border-top="1pt solid #308FCF">
		                <fo:page-number/>&#160;/&#160;<fo:page-number-citation  ref-id="theEnd" />
		            </fo:block>
		        </fo:static-content>
		        <fo:static-content flow-name="header">
		            <fo:block text-align="right" border-bottom="1pt solid #308FCF" color="#308FCF" font-style="italic">
						<xsl:value-of select="personalInfo/firstName" /> &#160; V.
		            </fo:block>
		        </fo:static-content>
				
				<fo:flow flow-name="xsl-region-body" >
					<fo:table>
						<fo:table-column column-width="20%"/>
						<fo:table-column column-width="80%"/>
						<fo:table-body>	
							<!-- Summary section -->
							<fo:table-row>
								<fo:table-cell number-columns-spanned="2" padding-left="25" padding="5">
									<fo:block font-weight="bold" color="#308FCF"><xsl:value-of select="summary/@title"/></fo:block>
								</fo:table-cell>
							</fo:table-row>
							<fo:table-row>
								<fo:table-cell number-columns-spanned="2">
									<fo:block text-align="justify"><xsl:value-of select="summary"/></fo:block>
								</fo:table-cell>
							</fo:table-row>
							
							<!-- Education section -->
							<fo:table-row>
								<fo:table-cell number-columns-spanned="2" padding-left="25" padding="5">
									<fo:block font-weight="bold" color="#308FCF"><xsl:value-of select="education/@title"/></fo:block>
								</fo:table-cell>
							</fo:table-row>
							<xsl:for-each select="education/organisation">
								<fo:table-row margin-left="5" margin-right="5"><!-- Date -->
									<fo:table-cell border-width="1" border-style="dotted" text-align="center" display-align="center">
										<fo:block><xsl:value-of select="date" /></fo:block>
									</fo:table-cell>
									<fo:table-cell border-width="1" border-style="dotted"><!-- Study description -->
										<fo:block color="#308FCF" margin-top="3"><xsl:value-of select="name" /><fo:inline color="black"> (<xsl:value-of select="geolocalization" />)</fo:inline>  </fo:block>
										<fo:block text-align="justify"><xsl:value-of select="speciality" />&#160; <xsl:value-of select="desc" /> </fo:block>
									</fo:table-cell>
								</fo:table-row>	
							</xsl:for-each>
							
							
							<!-- Professional experience section -->
							<fo:table-row>
								<fo:table-cell number-columns-spanned="2" padding-left="25" padding="5">
									<fo:block font-weight="bold" color="#308FCF"><xsl:value-of select="experiences/@title"/></fo:block>
								</fo:table-cell>
							</fo:table-row>
							<xsl:for-each select="experiences/company">
								<fo:table-row margin-left="5" margin-right="5"><!-- Date -->
									<fo:table-cell border-width="1" border-style="dotted" text-align="center" display-align="center">
										<fo:block><xsl:value-of select="date" /></fo:block>
									</fo:table-cell>
									<fo:table-cell border-width="1" border-style="dotted" >
										<fo:block margin-top="3"><xsl:value-of select="/cv/localization/label_CompanyName" />:&#160; <fo:inline color="#308FCF"><xsl:value-of select="name" /></fo:inline> (<xsl:value-of select="geolocalization" />)</fo:block>
										<fo:block><xsl:value-of select="/cv/localization/label_JobTitle" />:&#160;<fo:inline color="#308FCF"><xsl:value-of select="jobTitle" /></fo:inline></fo:block>
										<fo:block text-align="justify" margin-top="3px" margin-bottom="3px" margin-left="17px"><fo:inline font-weight="bold"><xsl:value-of select="/cv/localization/label_JobDecs" /></fo:inline>:&#160;<xsl:value-of select="jobDesc" />
										<xsl:if test="technologiesList"> <!-- we'll show the additional tech. section only if there is node technologiesList, for exemple, this section is ommited for DVK-Tech company -->
											<fo:inline font-weight="bold"><xsl:value-of select="/cv/localization/label_technologyList" /></fo:inline>:&#160;<fo:inline font-family="Courier" font-size="small"><xsl:value-of select="technologiesList" /></fo:inline>
										</xsl:if> 
										</fo:block>
									</fo:table-cell>
								</fo:table-row>	
							</xsl:for-each>
							
							<!-- Skills section -->
							<fo:table-row>
								<fo:table-cell number-columns-spanned="2" padding-left="25" padding="5">
									<fo:block font-weight="bold" color="#308FCF"><xsl:value-of select="technologies/@title"/></fo:block>
								</fo:table-cell>
							</fo:table-row>
							<xsl:for-each select="technologies/technology-domain">
								<fo:table-row margin-left="5" margin-right="5"><!-- Date -->
									<fo:table-cell border-width="1" border-style="dotted" text-align="center" display-align="center">
										<fo:block><xsl:value-of select="name" /></fo:block>
									</fo:table-cell>
									<fo:table-cell border-width="1" border-style="dotted" >
										<fo:block text-align="justify"><xsl:value-of select="itemsList" /></fo:block>
									</fo:table-cell>
								</fo:table-row>	
							</xsl:for-each>
							
							<!-- Language section -->
							<fo:table-row>
								<fo:table-cell number-columns-spanned="2" padding-left="25" padding="5">
									<fo:block font-weight="bold" color="#308FCF"><xsl:value-of select="languages/@title"/></fo:block>
								</fo:table-cell>
							</fo:table-row>
							<xsl:for-each select="languages/language">
								<fo:table-row margin-left="5" margin-right="5"><!-- Date -->
									<fo:table-cell border-width="1" border-style="dotted" text-align="center" display-align="center">
										<fo:block><xsl:value-of select="name" /></fo:block>
									</fo:table-cell>
									<fo:table-cell border-width="1" border-style="dotted" >
										<fo:block text-align="justify"><xsl:value-of select="desc" /></fo:block>
									</fo:table-cell>
								</fo:table-row>	
							</xsl:for-each>
						</fo:table-body>
					</fo:table>
					<fo:block id="theEnd" />
				</fo:flow>
			</fo:page-sequence>
		</fo:root>
	</xsl:template>
</xsl:stylesheet>
